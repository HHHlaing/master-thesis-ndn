hashfunction SHA1;
usertype ContentEncryptionKey, Content;

const pk: Function;
secret sk: Function;
inversekeys (pk,sk);

macro ccek =  { cek }pk(D);
macro c = { data , Rb }k(M,D);
macro msg = (M, c, ccek);

protocol HSSDataPull (M, D) 
{
	role M
	{
		fresh Rb: Nonce;

		send_1(M, D,  {  M, D, Rb  }sk(M) );

		var cek: ContentEncryptionKey;
		var data: Content;
		recv_2(D, M,   msg, {  SHA1(msg) }sk(D) );

		claim_M1(M, Alive);
		claim_M2(M, Secret, cek);
		claim_M3(M, Secret, data);
		claim_M4(M, Weakagree);
		claim_M5(M, Niagree);
		claim_M6(M, Nisynch);
	}

	role D
	{
		var Rb: Nonce;

		recv_1(M, D,  { M, D, Rb }sk(M) );
		
		fresh cek: ContentEncryptionKey;
		fresh data: Content;
		send_2(D, M,   msg,  {  SHA1(msg) }sk(D) );

		claim_D1(D, Alive);
		claim_D2(D, Secret, cek);
		claim_D3(D, Secret, data);
		claim_D4(D, Weakagree);
		claim_D5(D, Niagree);
		claim_D6(D, Nisynch);
	}
}